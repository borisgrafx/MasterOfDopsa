.data
arr: 
    .word 1, 4, 3, 9, 3, 2, 10, 6, 13, 15, 11, 1

.rodata
array_length:
    .word 12

.text
__start:
.globl __start
    la a0, array_length
    lw a1, 0(a0) # Загрузили длину массива в регистр a1
    addi a1, a1, -1 # Длина массива - 1
    lw a0, 0(a0) # Загрузили длину массива в регистр a0
    la a2, arr # Указатель
    add a5, x0, x0 # i = 0
L1:
        add a6, x0, a5 # least = i
        addi a4, a5, 1 # j = i + 1
L2:
        slli t2, a4, 2 # Логический сдвиг влево j на 2 разряда в ячейке t2
        slli t3, a6, 2 # Логический сдвиг влево least на 2 разряда в ячейке t3
        add t0, a2, t3 # Ячейка t0 = arr[t3]
        lw t6, 0(t0) # Загрузка в t6 значения массива arr[t3]
        add t0, a2, t2 # Ячейка t0 = arr[t2]
        lw t5, 0(t0) # Загрузка в t5 значения массива arr[t2]
        bge t5, t6, nochange # Если t5 >= t6, goto nochange
        add a6, x0, a4 # least = j
nochange:
        addi a4, a4, 1 # j = j + 1
        blt a4, a0, L2 # Если j < array_len, goto L2 
swap:
        slli t4, a5, 2 # Логический сдвиг влево i на 2 разряда в ячейке t4
        add t0, t4, a2 # Ячейка t0 = arr[t4] 
        lw t5, 0(t0) # Загрузка в t5 значения массива arr[t4] (a[i])
        slli t3, a6, 2 # Логический сдвиг влево least на 2 разряда в ячейке t3
        add t0, t3, a2 # Ячейка t0 = arr[t3]
        lw t6, 0(t0) # Загрузка в t6 значения массива arr[t3] (a[least])
        add t0, t4, a2 # Ячейка t0 = arr[t4]
        sw t6, 0(t0) # Загрузка в t6 значения массива arr[t4] (a[i])
        add t0, t3, a2 # Ячейка t0 = arr[t3]
        sw t5, 0(t0) # Загрузка в t5 значения массива arr[t3] (a[least])
        addi a5, a5, 1 # i += 1
        blt a5, a1, L1 # Если i < array_len - 1, goto L1
finish:
        li a0, 10
        ecall